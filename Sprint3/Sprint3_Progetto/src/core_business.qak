System -trace wis
 
// WIS
Dispatch start_robot : start_robot(X)

// op_robot   
Dispatch waiting : waiting(X)
Dispatch robot_info: robot_info(X,Y,POSITION,JOB)
 
// WASTE STORAGE
Dispatch waste_in : waste_in(X)
Dispatch waste_qty : waste_qty(X)
Dispatch get_waste : get_waste(X)

// INCINERATOR
Dispatch turn_on : turn_on(X)
Dispatch burn_in : burn_in(X)
Dispatch burn_start : burn_start(X)
Dispatch burn_end : burn_end(X)
Dispatch get_ash : get_ash(X)

// ASH STORAGE
Dispatch deposit_ash : deposit_ash(X)
Request empty_ash : empty_ash(X)
Reply ashes_taken : ashes_taken(LEVEL) for empty_ash

// BASIC ROBOT
Request moverobot    :  moverobot(TARGETX, TARGETY)
Reply moverobotdone  :  moverobotok(ARG)
Reply moverobotfailed:  moverobotfailed(PLANDONE, PLANTODO)
Dispatch setdirection : dir( D )
Dispatch setrobotstate: setpos(X,Y,D)

Request engage        : engage(OWNER,STEPTIME)	
Reply   engagedone    : engagedone(ARG) for engage 
Reply   engagerefused : engagerefused(ARG) for engage 

Dispatch disengage    : disengage(ARG)
//LED
Dispatch led_status : led_status(X)

//SONAR
Dispatch ash_level : ash_level(LEVEL)
Event sonardata    : distance(D)   //emitted  by sonardevice  

Dispatch sonarwork : sonarwork(X)
Dispatch doread    : doread(X)

//SSGUI
Dispatch guidata: guidata(WASTE_QTY,ASH_LEVEL,ISBURNING,X,Y,POSITION,JOB)

Context ctx_wis  ip  [host="localhost" port=8014]
Context ctx_basic_robot ip  [host="127.0.0.1" port=8020]
Context ctx_monitoring_device  ip  [host="127.0.0.2" port=8021]

ExternalQActor basicrobot context ctx_basic_robot
ExternalQActor sonar context ctx_monitoring_device
ExternalQActor led context ctx_monitoring_device

QActor mock context ctx_wis {
	State s0 initial {
		delay 500
		forward waste_storage -m waste_in:waste_in(1)	
		delay 15000
		request ash_storage -m empty_ash:empty_ash(1)
	}
	
	Transition t0 whenReply ashes_taken -> reply
	
	State reply  {
		delay 15000
	}
	Goto s0
}
 
QActor wis context ctx_wis {
	[#
			var ash_qty = 0
			var waste_qty = 0
			var ash_level = 0
			var isBurning: Boolean = false;
			var robot_waiting: Boolean = false;
			val DLIMT: Int = 5;
			val DMIN: Int = 25;
			var x: Int = 0;
			var y: Int = 0;
			var position: String = "";
			var job: String = "";
	#]
	State s0 initial{ 
		[#ash_level= DMIN#]
		delay 500
		observeResource waste_storage msgid waste_qty
		observeResource sonar msgid ash_level
		observeResource op_robot msgid robot_info
		forward incinerator -m turn_on:turn_on(1)
        println("$name INIZIATO") color green
	}
	
	Transition t0
		whenMsg waiting -> robotWaiting
		whenMsg waste_qty -> updateWasteQty
		whenMsg burn_start -> startBurningPhase
		whenMsg burn_end -> endBurningPhase
		whenMsg ash_level -> updateAshLevel
		whenMsg robot_info -> updateGUI
	
	
	State updateGUI{
		onMsg(robot_info:robot_info(X,Y,POSITION,JOB)){
			[#
				x = payloadArg(0).toInt();
				y = payloadArg(1).toInt();
				position = payloadArg(2).toString();
				job = payloadArg(3).toString();
			#]
			
		}
	}
	Goto verifyConditions
	
	State updateAshLevel{
		onMsg(ash_level:ash_level(N)){
			[# ash_level = payloadArg(0).toInt();#]
			if [# ash_level < DLIMT || ash_level >= DMIN #]
			{
				forward led -m led_status:led_status(BLINK)
			}			
		}
	}
	Goto verifyConditions
	
	State robotWaiting{
		[# robot_waiting = true #]
	}
	
	Goto verifyConditions 
	
	State updateWasteQty{
		onMsg(waste_qty:waste_qty(N)){
			[# waste_qty = payloadArg(0).toInt()/50 #]
		}
	}
	
	Goto verifyConditions
		
	State startBurningPhase{
		[# isBurning = true #]
		forward led -m led_status:led_status(ON)
	}
	
	Goto verifyConditions
		
	State endBurningPhase{
		[# isBurning = false #]
		forward led -m led_status:led_status(OFF)
	}
	
	Goto verifyConditions
	
	State verifyConditions{
		updateResource[#"guidata($waste_qty,$ash_level,$isBurning,$x,$y,$position,$job)"#] 
		if [# robot_waiting && !isBurning && waste_qty > 0 && ash_level >= DLIMT#]{
			
			forward op_robot -m start_robot:start_robot(1)		
			[# robot_waiting = false #]
		}
	}
	
	Transition t0
		whenMsg waiting -> robotWaiting
		whenMsg waste_qty -> updateWasteQty
		whenMsg burn_start -> startBurningPhase
		whenMsg burn_end -> endBurningPhase
		whenMsg ash_level -> updateAshLevel
		whenMsg robot_info -> updateGUI
	
}

 


  
QActor op_robot context ctx_wis  { 
	import "wis.util.*"
	[#  
		var RP: Boolean = false;
		var ASH: Boolean = false;
		var T: String = "home"; 
		var X = "0";
		var Y = "0";
		var D = "";
	
	#]
	 
	State s0 initial { 
		delay 500
		println("$name INIZIATO") color green 
		solve(consult("sysRules.pl"))
		solve(consult("pointPicker.pl"))
		solve(consult("positions.pl"))
	}
	
	Goto engage
	
	State engage {
		
		request basicrobot -m engage:engage($MyName,300)
		
	}
	
	Transition t0 whenReply engagedone -> waiting
		whenReply engagerefused -> engage
	 
	State waiting {
		forward wis -m waiting:waiting(1)
		updateResource [# "robot_info($X,$Y,$T,waiting for wis permission)"#]
	}
	
	Transition t0 whenMsg start_robot -> to_ws
	
	State to_ws {
		updateResource [# "robot_info($X,$Y,$T,moving to waste storage)"#]
		[# T = Position.wastein.name; #]
		solve( getPoint($T,TX,TY,TDIR) )
		[# X = getCurSol("TX").toString(); #]
		[# Y = getCurSol("TY").toString(); #]
		[# D = getCurSol("TDIR").toString(); #]
		
		request basicrobot -m moverobot:moverobot($X,$Y)
		delay 500
		forward basicrobot -m setdirection:dir($D)
	}
	
	Transition t1 whenReply moverobotdone -> withdraw_ws
				  
				  
    State withdraw_ws {
    	updateResource [# "robot_info($X,$Y,$T,withdrawing rp from waste storage)"#]
    	delay 1500
    	forward waste_storage -m get_waste:get_waste(1)	
    	[# RP = true; #]
    }
    
    Goto to_incinerator
    
    State to_incinerator {
    	updateResource [# "robot_info($X,$Y,$T,moving to burnin)"#]
    	[# T = Position.burnin.name; #]
		solve( getPoint($T,TX,TY,TDIR) )
		[# X = getCurSol("TX").toString(); #]
		[# Y = getCurSol("TY").toString(); #]
		[# D = getCurSol("TDIR").toString(); #]
		
		request basicrobot -m moverobot:moverobot($X,$Y)
		delay 500
		forward basicrobot -m setdirection:dir($D)
    }
    
    Transition t2 whenReply moverobotdone -> ask_to_burn
				  whenReply moverobotfailed -> to_incinerator

	State ask_to_burn {
		updateResource [# "robot_info($X,$Y,$T,depositing rp in incinerator)"#]
		delay 1500
		forward incinerator -m burn_in:burn_in(1)
		[# RP = false #]
	}
	
	Goto go_wait_home
	
	State go_wait_home {
		updateResource [# "robot_info($X,$Y,$T,moving to home)"#]
		[# T = Position.home.name; #]
		solve( getPoint($T,TX,TY,TDIR) )
		[# X = getCurSol("TX").toString(); #]
		[# Y = getCurSol("TY").toString(); #]
		[# D = getCurSol("TDIR").toString(); #]
		
		request basicrobot -m moverobot:moverobot($X,$Y)
		delay 500
		forward basicrobot -m setdirection:dir($D)
	}
    
    Transition t3 whenReply moverobotdone -> wait_for_burn
				  whenReply moverobotfailed -> go_back_home
				  
	State wait_for_burn {
		updateResource [# "robot_info($X,$Y,$T,waiting at home)"#]
		delay 1500
	}
	
	Transition t4 whenMsg burn_end -> to_incinerator_burned
	
	State to_incinerator_burned {
		updateResource [# "robot_info($X,$Y,$T,moving to burnout port)"#]
		[# T = Position.burnout.name; #]
		solve( getPoint($T,TX,TY,TDIR) )
		[# X = getCurSol("TX").toString(); #]
		[# Y = getCurSol("TY").toString(); #]
		[# D = getCurSol("TDIR").toString(); #]
		
		request basicrobot -m moverobot:moverobot($X,$Y)
		delay 500
		forward basicrobot -m setdirection:dir($D)
	}
	
	Transition t5 whenReply moverobotdone -> get_ash
				  whenReply moverobotfailed -> to_incinerator_burned
	
	State get_ash {
		updateResource [# "robot_info($X,$Y,$T,withdrawing ash from incinerator)"#]
		delay 1500
		forward incinerator -m get_ash:get_ash(1)
		[# ASH = true #] 
	}
	
	Goto to_ash_storage
	
	State to_ash_storage {
		updateResource [# "robot_info($X,$Y,$T,moving to ash storage)"#]
		[# T = Position.ashout.name; #]
		solve( getPoint($T,TX,TY,TDIR) )
		[# X = getCurSol("TX").toString(); #]
		[# Y = getCurSol("TY").toString(); #]
		[# D = getCurSol("TDIR").toString(); #]
		
		request basicrobot -m moverobot:moverobot($X,$Y)
		delay 500
		forward basicrobot -m setdirection:dir($D)
	}
	
	Transition t6 whenReply moverobotdone -> ask_as
				  whenReply moverobotfailed -> to_ash_storage
	
	State ask_as {
		updateResource [# "robot_info($X,$Y,$T,depositing ash in ash storage)"#]
		delay 1500
		forward ash_storage -m deposit_ash:deposit_ash(1)
		[# ASH = false #] 
		
	}
	Goto go_back_home
	
	State go_back_home {
		updateResource [# "robot_info($X,$Y,$T,going back home)"#]
		[# T = Position.home.name; #]
		solve( getPoint($T,TX,TY,TDIR) )
		[# X = getCurSol("TX").toString(); #]
		[# Y = getCurSol("TY").toString(); #]
		[# D = getCurSol("TDIR").toString(); #]
		
		
		forward basicrobot -m setdirection:dir(up)
		delay 500
		request basicrobot -m moverobot:moverobot($X,$Y)
	}
	
	Transition t6 whenReply moverobotdone -> waiting
				  whenReply moverobotfailed -> go_back_home
}


QActor incinerator context ctx_wis{
	[# 
			var RP: Int = 0;
			val BTIME: Long = 4000L;
	#]
	
	State s0 initial { 
		delay 500
		println("$name INIZIATO") color green
	}
	
	Transition t0 whenMsg turn_on -> ready
	
	State ready {
		println("$name Acceso inceneritore")
	}
	
	Transition t1 whenMsg burn_in -> burn_rp
	
	State burn_rp {
		[# var RP = 1; #]
		forward wis -m burn_start:burn_start(1)
		delayVar BTIME
		
		forward wis -m burn_end:burn_end(1)
		forward op_robot -m burn_end:burn_end(1)
	}
	
	Transition t1 whenMsg get_ash -> get_ash
	
	State get_ash {
		[# var RP = 0; #]
	}
	
	Goto ready
}


QActor ash_storage context ctx_wis{
	[#
			var ash_qty: Int = 0;
			var R: Int = 0;
			val MAX: Int = 4;
	#]
	
	State s0 initial {
		delay 500
		println("$name INIZIATO") color green
	}
	
	Transition t0 
		whenRequest empty_ash -> removeAsh
		whenMsg deposit_ash -> addAsh
	
	State addAsh{
		if[#ash_qty<MAX#]{
			[#	ash_qty++  #]
			println("$name Depositata la cenere di un RP!")
		}else{
			println("$name Deposito pieno! ")
		}
	}
	Transition t0 
		whenRequest empty_ash -> removeAsh
		whenMsg deposit_ash -> addAsh
	
	State removeAsh{
		[# R = ash_qty; #]
		[# ash_qty = 0; #]
		
		replyTo empty_ash with ashes_taken : ashes_taken($R)
		println("$name Prelevata $R cenere")
	}
}


QActor waste_storage context ctx_wis{
	[#
			var scale: Int = 0;
	#]
	
	State s0 initial{ 
		delay 500
		println("$name INIZIATO") color green
	}
	
	Transition t0 
		whenMsg waste_in -> addRP
		whenMsg get_waste -> removeRP
	
	State addRP{
		[#	scale += 50;  #]
		updateResource [# "waste_qty($scale)" #]
		println("$name Depositato un RP!")
	}
	Transition t0 
		whenMsg waste_in -> addRP
		whenMsg get_waste -> removeRP
	
	State removeRP{
		[#	scale -= 50  #]
		updateResource [# "scaleinfo($scale)" #]
		println("$name Prelevato un RP!")
	}
}
Facade robotfacade port 8060 usingactor wis inctx ctx_wis  
                   appl "main.java.unibo.sprint3_progetto_ssgui.Sprint3ProgettoSsguiApplication" 
				   UserLibs -f "sprint3_progetto_ssgui-1.0-plain"  -f "sprint3_progetto_ssgui-1.0"
